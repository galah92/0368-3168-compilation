import java.io.PrintWriter;
import java_cup.runtime.*;
import AST.*;

parser code 
{:
    public Lexer lexer;
    public PrintWriter writer;

    public Parser(Lexer lexer, PrintWriter writer)
    {
        this.lexer = lexer;
        this.writer = writer;
    }
    public void report_error(String message, Object info)
    {
        writer.println("ParserError(" + lexer.getLine() + ", " + lexer.getCharPos() + "): " + message);
        writer.close();
        System.exit(0);
    }
:}


/* CUSTOMIZE CUP PARSER */
scan with
{:
    return lexer.next_token();
:};


/* TERMINALS */
terminal LPAREN;
terminal RPAREN;
terminal LBRACK;
terminal RBRACK;
terminal LBRACE;
terminal RBRACE;
terminal NIL;
terminal PLUS;
terminal MINUS;
terminal TIMES;
terminal DIVIDE;
terminal COMMA;
terminal DOT;
terminal SEMICOLON;
terminal ASSIGN;
terminal EQ;
terminal LT;
terminal GT;
terminal ARRAY;
terminal CLASS;
terminal EXTENDS;
terminal RETURN;
terminal WHILE;
terminal IF;
terminal NEW;
terminal COMMENT;

terminal String STRING;
terminal String ID;
terminal Integer INT;

terminal ILLEGAL_CHARACTER;
terminal ILLEGAL_INTEGER;
terminal ILLEGAL_COMMENT;

/* NON TERMINALS */
non terminal AST_DecList decList;
non terminal AST_Dec dec;
non terminal AST_VarDec varDec;
non terminal AST_FuncDec funcDec;
non terminal AST_ClassDec classDec;
non terminal AST_ArrayDec arrayDec;
non terminal AST_ParamsList paramsList;
non terminal AST_ClassField cField;
non terminal AST_ClassFieldList cFieldList;
non terminal AST_Exp exp;
non terminal AST_Var var;
non terminal AST_ExpCall callExp;
non terminal AST_ExpBinOp binopExp;
non terminal AST_ExpPrimitive primitiveExp;
non terminal AST_ExpList expList;
non terminal AST_NewExp newExp;
non terminal AST_Stmt stmt;
non terminal AST_StmtList stmtList;

/* OPERATOR PRECEDENCE */
precedence nonassoc ASSIGN;
precedence left     EQ;
precedence left     LT;
precedence left     GT;
precedence left     PLUS;
precedence left     MINUS;
precedence left     TIMES;
precedence left     DIVIDE;
precedence nonassoc LBRACK;
precedence nonassoc LPAREN;
precedence left     DOT;


start with decList;


paramsList  ::=     ID:id1 ID:id2 COMMA paramsList:list {: RESULT = new AST_ParamsList(id1, id2, list); RESULT.lineNumber = id1left; :} |
                    ID:id1 ID:id2                       {: RESULT = new AST_ParamsList(id1, id2, null); RESULT.lineNumber = id1left; :} ;

cFieldList  ::=     cField:field cFieldList:list        {: RESULT = new AST_ClassFieldList(field, list); RESULT.lineNumber = fieldleft; :} |
                    cField:field                        {: RESULT = new AST_ClassFieldList(field, null); RESULT.lineNumber = fieldleft; :} ;

expList     ::=     exp:e COMMA expList:list            {: RESULT = new AST_ExpList(e, list); RESULT.lineNumber = eleft; :} |
                    exp:e                               {: RESULT = new AST_ExpList(e, null); RESULT.lineNumber = eleft; :} ;

decList		::=		dec:d decList:l	{: RESULT = new AST_DecList(d, l); RESULT.lineNumber = dleft; :} |
                    dec:d			{: RESULT = new AST_DecList(d, null); RESULT.lineNumber = dleft; :}
                    ;

dec			::=		varDec:d		{: RESULT = d; RESULT.lineNumber = dleft; :} |
                    funcDec:d		{: RESULT = d; RESULT.lineNumber = dleft; :} |
                    classDec:d		{: RESULT = d; RESULT.lineNumber = dleft; :} |
                    arrayDec:d		{: RESULT = d; RESULT.lineNumber = dleft; :}
                    ;
varDec		::=		ID:type ID:var SEMICOLON	                {: RESULT = new AST_VarDec(type, var, null); RESULT.lineNumber = typeleft; :} |
                    ID:type ID:var ASSIGN exp:e SEMICOLON       {: RESULT = new AST_VarDec(type, var, e); RESULT.lineNumber = typeleft; :} |
                    ID:type ID:var ASSIGN newExp:e SEMICOLON    {: RESULT = new AST_VarDec(type, var, e); RESULT.lineNumber = typeleft; :}
                    ;
funcDec		::=		ID:ret ID:name LPAREN paramsList:p RPAREN LBRACE stmtList:body RBRACE   {: RESULT = new AST_FuncDec(ret, name, p, body); RESULT.lineNumber = retleft; :} |
                    ID:ret ID:name LPAREN RPAREN LBRACE stmtList:body RBRACE                {: RESULT = new AST_FuncDec(ret,name,null,body); RESULT.lineNumber = retleft; :}
                    ;
classDec	::=		CLASS ID:name LBRACE cFieldList:l RBRACE	                {: RESULT = new AST_ClassDec(name, null, l); RESULT.lineNumber = lleft; :} |
                    CLASS ID:name EXTENDS ID:base LBRACE cFieldList:l RBRACE	{: RESULT = new AST_ClassDec(name, base, l); RESULT.lineNumber = lleft; :}
                    ;

arrayDec    ::=     ARRAY ID:name EQ ID:Type LBRACK RBRACK                        {: RESULT = new AST_ArrayDec(name, Type); RESULT.lineNumber = nameleft; :};

exp			::=		var:v		            {: RESULT = v; RESULT.lineNumber = vleft; :} |
                    LPAREN exp:e RPAREN		{: RESULT = e; RESULT.lineNumber = eleft; :} |
                    binopExp:e	            {: RESULT = e; RESULT.lineNumber = eleft; :} |
                    callExp:e		        {: RESULT = e; RESULT.lineNumber = eleft; :} |
                    primitiveExp:e          {: RESULT = e; RESULT.lineNumber = eleft; :}
                    ;

var		::=		    ID:name						{: RESULT = new AST_VarSimple(name); RESULT.lineNumber = nameleft; :} |
                    var:v DOT ID:fieldName		{: RESULT = new AST_VarClassField(v, fieldName); RESULT.lineNumber = vleft; :} |
                    var:v LBRACK exp:e RBRACK	{: RESULT = new AST_VarArrayElement(v,e); RESULT.lineNumber = vleft; :}
                    ;

binopExp	::=		exp:e1 PLUS exp:e2		{: RESULT = new AST_ExpBinOp(e1, e2, '+'); RESULT.lineNumber = e1left; :} |
                    exp:e1 MINUS exp:e2 	{: RESULT = new AST_ExpBinOp(e1, e2, '-'); RESULT.lineNumber = e1left; :} |
                    exp:e1 TIMES exp:e2 	{: RESULT = new AST_ExpBinOp(e1, e2, '*'); RESULT.lineNumber = e1left; :} |
                    exp:e1 DIVIDE exp:e2 	{: RESULT = new AST_ExpBinOp(e1, e2, '/'); RESULT.lineNumber = e1left; :} |
                    exp:e1 LT exp:e2		{: RESULT = new AST_ExpBinOp(e1, e2, '<'); RESULT.lineNumber = e1left; :} |
                    exp:e1 GT exp:e2		{: RESULT = new AST_ExpBinOp(e1, e2, '>'); RESULT.lineNumber = e1left; :} |
                    exp:e1 EQ exp:e2		{: RESULT = new AST_ExpBinOp(e1, e2, '='); RESULT.lineNumber = e1left; :}
                    ;

callExp		::=		ID:funcName LPAREN RPAREN                           {: RESULT = new AST_ExpCall(funcName, null, null); RESULT.lineNumber = funcNameleft; :} |
                    ID:funcName LPAREN expList:args RPAREN              {: RESULT = new AST_ExpCall(funcName, null, args); RESULT.lineNumber = funcNameleft; :} |
                    var:v DOT ID:funcName LPAREN RPAREN                 {: RESULT = new AST_ExpCall(funcName, v, null); RESULT.lineNumber = funcNameleft; :} |
                    var:v DOT ID:funcName LPAREN expList:args RPAREN    {: RESULT = new AST_ExpCall(funcName, v, args); RESULT.lineNumber = funcNameleft; :}
                    ;

primitiveExp    ::=     INT:i           {: RESULT = new AST_ExpInt(i); RESULT.lineNumber = ileft; :} |
                        MINUS INT:i     {: RESULT = new AST_ExpInt(-i); RESULT.lineNumber = ileft; :} |
                        STRING:s        {: RESULT = new AST_ExpString(s); RESULT.lineNumber = sleft; :} |
                        NIL:n           {: RESULT = new AST_ExpNil(); RESULT.lineNumber = nleft; :}
                        ;

stmtList	::=		stmt:s stmtList:l	{: RESULT = new AST_StmtList(s,l); RESULT.lineNumber = sleft; :} |
                    stmt:s  			{: RESULT = new AST_StmtList(s,null); RESULT.lineNumber = sleft; :}
                    ;
                    
stmt		::=		varDec:v                                                    {: RESULT = new AST_StmtVarDec(v); RESULT.lineNumber = vleft; :} |
                    var:v ASSIGN exp:e SEMICOLON						    	{: RESULT = new AST_StmtAssign(v,e); RESULT.lineNumber = vleft; :} |
                    var:v ASSIGN newExp:e SEMICOLON						    	{: RESULT = new AST_StmtAssign(v,e); RESULT.lineNumber = vleft; :} |
                    RETURN:r SEMICOLON									        {: RESULT = new AST_StmtReturn(null); RESULT.lineNumber = rleft; :} |
                    RETURN exp:e SEMICOLON									    {: RESULT = new AST_StmtReturn(e); RESULT.lineNumber = eleft; :} |
                    IF    LPAREN exp:cond RPAREN LBRACE stmtList:body RBRACE	{: RESULT = new AST_StmtIf(cond,body); RESULT.lineNumber = condleft; :} |
                    WHILE LPAREN exp:cond RPAREN LBRACE stmtList:body RBRACE	{: RESULT = new AST_StmtWhile(cond,body); RESULT.lineNumber = condleft; :} |
                    callExp:callExp SEMICOLON									{: RESULT = new AST_StmtCall(callExp); RESULT.lineNumber = callExpleft; :}
                    ;

newExp      ::=     NEW ID:type                         {: RESULT = new AST_NewExp(type, null); RESULT.lineNumber = typeleft; :} |
                    NEW ID:type LBRACK exp:e RBRACK     {: RESULT = new AST_NewExp(type, e); RESULT.lineNumber = typeleft; :}
                    ;

cField      ::=     varDec:d                         {: RESULT = d; RESULT.lineNumber = dleft; :} |
                    funcDec:d                        {: RESULT = d; RESULT.lineNumber = dleft; :} ;
